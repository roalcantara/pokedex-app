// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
  // Define versions in a single place
  ext {
    kotlin_version = '1.7.0'
    gradle_plugin_version = '7.3.0-beta05'

    ktlint_plugin_version = '10.3.0'
    compose_snapshot_version = ''
    ktlint_version = '0.45.2'
  }

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    // NOTE: Do not place your application dependencies here; they belong
    // in the individual module build.gradle files
    classpath "com.android.tools.build:gradle:$gradle_plugin_version"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

plugins {
  id 'com.android.application' version "$gradle_plugin_version" apply false
  id 'com.android.library' version "$gradle_plugin_version" apply false
  id 'org.jetbrains.kotlin.android' version "$kotlin_version" apply false
  id 'org.jlleitschuh.gradle.ktlint' version "$ktlint_plugin_version"
}

subprojects {
  repositories {
    google()
    mavenCentral()

    if (!compose_snapshot_version.isEmpty()) {
      maven {
        url "https://androidx.dev/snapshots/builds/" +
          "${compose_snapshot_version}/artifacts/repository/"
      }
    }
  }

  apply plugin: 'org.jlleitschuh.gradle.ktlint'
  ktlint {
    version = "$ktlint_version"
    debug = true
    android = true
    enableExperimentalRules = true
    additionalEditorconfigFile = file("$rootDir/.editorconfig")
  }
}
